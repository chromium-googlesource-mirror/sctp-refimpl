define allep
set $_inp=sctppcbinfo.listhead.lh_first
while ( $_inp != 0 )
print $_inp
set $_inp=$_inp->sctp_list.le_next
end
end
document allep
Show all SCTP endpoints
end
define allreadq
set $_inp=sctppcbinfo.listhead.lh_first
while ( $_inp != 0 )
print $_inp->read_queue.tqh_first
set $_inp=$_inp->sctp_list.le_next
end
end
document allreadq
Show all SCTP endpoint read queues
end


define ps
set $_p=allproc.lh_first
set $index=0
while ( $_p != 0 )
printf "process %d is %s\n", $index,$_p->p_comm
set $index=$index + 1
set $_p=$_p->p_list.le_next
end
end
document ps
Display all processes and their index
end


define getps
set $_p=allproc.lh_first
set $index=0
while ( $_p != 0 )
if ($index == $arg0)
 printf "process %d is %s\n", $index,$_p->p_comm
 print $_p
end
set $index=$index + 1
set $_p=$_p->p_list.le_next
end
end
document ps
Get process <index> as a variable to use
end



define allreadq
set $_inp=sctppcbinfo.listhead.lh_first
while ( $_inp != 0 )
print $_inp->read_queue.tqh_first
set $_inp=$_inp->sctp_list.le_next
end
end
document allreadq
Show all SCTP endpoint read queues
end



define allasoc
set $_inp=sctppcbinfo.listhead.lh_first
while ( $_inp != 0 )
 set $_asoc=$_inp->sctp_asoc_list.lh_first
 while ( $_asoc != 0) 
    print $_asoc
    set $_asoc=$_asoc->sctp_tcblist.le_next
  end
 set $_inp=$_inp->sctp_list.le_next
end
end
document allasoc
Show all SCTP associations
end

define findep
set $_inp=sctppcbinfo.listhead.lh_first
while ( $_inp != 0 )
  if ($_inp->ip_inp.inp.inp_inc.inc_ie.ie_lport == $arg0)
    print $_inp
    set $_inp=0
  else
    set $_inp=$_inp->sctp_list.le_next
  end
 end
end
document findep
find an endpoint
end

define findasoc
set $_inp=sctppcbinfo.listhead.lh_first
 while ( $_inp != 0 )
  if ($_inp->ip_inp.inp.inp_inc.inc_ie.ie_lport == $arg0)
     print $_inp
     set $_asoc=$_inp->sctp_asoc_list.lh_first
     while ( $_asoc != 0) 
        if ($_asoc->rport == $arg1)
           print $_asoc
           set $_asoc=0
        else
           set $_asoc=$_asoc->sctp_tcblist.le_next
        end
     end
    set $_inp=0
  else
    set $_inp=$_inp->sctp_list.le_next
  end
 end
end
document findasoc
Find an SCTP association srcport dstport
end


define interesting
printf "asociation %X\n", $arg0
printf "output_q_size %d\n", $arg0->asoc.total_output_queue_size
printf "size on streams %d\n",  $arg0->asoc.size_on_all_streams
printf "size on reasm %d\n", $arg0->asoc.size_on_reasm_queue
printf "send q cnt %d\n", $arg0->asoc.send_queue_cnt
printf "sent q cnt %d\n", $arg0->asoc.sent_queue_cnt
printf "ctrl q %x\n", $arg0->asoc.control_send_queue.tqh_first
end
document interesting
The interesting stuff I want to show on an assoc
end

define allasoc
set $_inp=sctppcbinfo.listhead.lh_first
while ( $_inp != 0 )
 set $_asoc=$_inp->sctp_asoc_list.lh_first
 while ( $_asoc != 0) 
    interesting $_asoc
    set $_asoc=$_asoc->sctp_tcblist.le_next
  end
 set $_inp=$_inp->sctp_list.le_next
end
end
document allasoc
Show some things on all SCTP associations
end


